{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

() recv_internal() {
}

(int) ubits(int v) impure asm "UBITSIZE";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {

  int flen = ubits(flag);
  int vlen = ubits(value);

  int flagbuffer = 0;
  int flagbufferlen = 0;
  int fmask = ~ (1 << flen);

  int writebuffer = 0;
  int writebufferlen = 0;

  int rbits = 0;

  tuple stack = null();
  tuple resstack = null();
  builder b = begin_cell();

  stack~push_back(linked_list.begin_parse());

  while (~ stack.is_null()) {
    slice s = stack~pop_back();

    while (~ s.slice_data_empty?()) {
      int bit = s~load_uint(1);
      rbits = flen - flagbufferlen - 1;
      flagbuffer += (bit << rbits);
      flagbufferlen += 1;

      if (rbits == 0) {
        int builderrbits = 1023 - b.builder_bits();
        if flagbuffer == flag {
          writebuffer = value;
          writebufferlen = vlen;
          flagbuffer = 0;
          flagbufferlen = 0;
        } else {
          writebuffer = flagbuffer >> flen - 1;
          writebufferlen = 1;
          flagbuffer = (flagbuffer << 1) & fmask;
          flagbufferlen -= 1;
        }

        if builderrbits > writebufferlen {
          b~store_uint(writebuffer, writebufferlen);
        } else {
          resstack~push_back(b);
          b = begin_cell().store_uint(writebuffer, writebufferlen);
        }
      }
    }

    if (~ s.slice_refs_empty?()) {
      stack~push_back(s~load_ref().begin_parse());
    }
  }

  if (1023 - b.builder_bits()) > flagbufferlen {
    b~store_uint(flagbuffer >> flen - flagbufferlen, flagbufferlen);
  } else {
    resstack~push_back(b);
    b = begin_cell().store_uint(flagbuffer >> flen - flagbufferlen, flagbufferlen);
  }

  resstack~push_back(b);

;;   if (resstack.is_null()) {
;;     return begin_cell().end_cell();
;;   }

  builder res = resstack~pop_back();

  while (~ resstack.is_null()) {
    res = resstack~pop_back().store_ref(res.end_cell());
  }

  return res.end_cell();

}

